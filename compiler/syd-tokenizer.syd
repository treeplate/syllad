import 'core.syd';
import 'syd-utils.syd';

// Tokenizer

Set keywords = Set();
keywords.add('is');
keywords.add('as');
keywords.add('__as__');
keywords.add('__dynamic_is__');
keywords.add('__dynamic_as__');

enum ts { // TokenState
  Top
  Identifier 
  SingleQuoteString 
  SingleQuoteStringCR 
  SingleQuoteStringEscape 
  DoubleQuoteString 
  DoubleQuoteStringCR 
  DoubleQuoteStringEscape
  Zero
  Integer
  HexStart
  Hex

  Period
  PeriodPeriod

  Plus
  Minus
  Asterisk
  AsteriskAsterisk
  Slash 
  Modulus
  Equals
  Bang
  LessThan 
  GreaterThan 
  Ampersand
  AmpersandAmpersand
  Caret
  VerticalBar
  VerticalBarVerticalBar

  LineComment 
  BlockComment
  BlockCommentCR
  BlockCommentAsterisk
  CR 
}

enum tt { // TokenType
  Identifier // String
  Keyword // String
  String // String
  Integer // Integer
  OpenBrace
  CloseBrace
  OpenBracket
  CloseBracket
  OpenParen
  CloseParen
  Period
  Ellipsis
  Comma
  Colon
  Semicolon

  Plus
  PlusEquals
  Minus
  MinusEquals
  Asterisk
  AsteriskEquals
  AsteriskAsterisk
  AsteriskAsteriskEquals
  Slash
  SlashEquals
  Modulus
  ModulusEquals
  Equals
  EqualsEquals
  Bang
  BangEquals
  LessThan
  LessThanLessThan
  LessThanEquals
  GreaterThan
  GreaterThanGreaterThan
  GreaterThanEquals
  Ampersand
  AmpersandEquals
  AmpersandAmpersand
  AmpersandAmpersandEquals
  Caret
  CaretEquals
  VerticalBar
  VerticalBarEquals
  VerticalBarVerticalBar
  VerticalBarVerticalBarEquals
  Tilde

  Eof
}

class Token extends Object {
  tt type;
  SourceLocation sourceLocation;
  Anything data;

  Null constructor(tt typeArg, SourceLocation sourceLocationArg, Anything dataArg) {
    super.constructor();
    if (typeArg == tt.Identifier && keywords.has(dataArg as String)) {
      type = tt.Keyword;
    } else {
      type = typeArg;
    }
    sourceLocation = sourceLocationArg;
    data = dataArg;
  }

  //#override
  String toString() {
    if (type == tt.Identifier) {
      return concat('identifier "', data, '"');
    }
    if (type == tt.Keyword) {
      return concat('keyword "', data, '"');
    }
    if (type == tt.String) {
      return concat('string literal');
    }
    if (type == tt.Integer) {
      return concat('integer literal "', data, '"');
    }

    if (type == tt.OpenBrace) {
      return '"{"';
    }
    if (type == tt.CloseBrace) {
      return '"}"';
    }
    if (type == tt.OpenBracket) {
      return '"["';
    }
    if (type == tt.CloseBracket) {
      return '"]"';
    }
    if (type == tt.OpenParen) {
      return '"("';
    }
    if (type == tt.CloseParen) {
      return '")"';
    }
    if (type == tt.Period) {
      return '"."';
    }
    if (type == tt.Ellipsis) {
      return '"..."';
    }
    if (type == tt.Comma) {
      return '","';
    }
    if (type == tt.Colon) {
      return '":"';
    }
    if (type == tt.Semicolon) {
      return '";"';
    }

    if (type == tt.Plus) {
      return '"+"';
    }
    if (type == tt.PlusEquals) {
      return '"+="';
    }
    if (type == tt.Minus) {
      return '"-"';
    }
    if (type == tt.MinusEquals) {
      return '"-="';
    }
    if (type == tt.Asterisk) {
      return '"*"';
    }
    if (type == tt.AsteriskEquals) {
      return '"*="';
    }
    if (type == tt.AsteriskAsterisk) {
      return '"**"';
    }
    if (type == tt.AsteriskAsteriskEquals) {
      return '"**="';
    }
    if (type == tt.Slash) {
      return '"/"';
    }
    if (type == tt.SlashEquals) {
      return '"/="';
    }
    if (type == tt.Modulus) {
      return '"%"';
    }
    if (type == tt.ModulusEquals) {
      return '"%="';
    }
    if (type == tt.Equals) {
      return '"="';
    }
    if (type == tt.EqualsEquals) {
      return '"=="';
    }
    if (type == tt.Bang) {
      return '"!"';
    }
    if (type == tt.BangEquals) {
      return '"!="';
    }
    if (type == tt.LessThan) {
      return '"<"';
    }
    if (type == tt.LessThanLessThan) {
      return '"<<"';
    }
    if (type == tt.LessThanEquals) {
      return '"<="';
    }
    if (type == tt.GreaterThan) {
      return '">"';
    }
    if (type == tt.GreaterThanGreaterThan) {
      return '">>"';
    }
    if (type == tt.GreaterThanEquals) {
      return '">="';
    }
    if (type == tt.Ampersand) {
      return '"&"';
    }
    if (type == tt.AmpersandEquals) {
      return '"&="';
    }
    if (type == tt.AmpersandAmpersand) {
      return '"&&"';
    }
    if (type == tt.AmpersandAmpersandEquals) {
      return '"&&="';
    }
    if (type == tt.Caret) {
      return '"^"';
    }
    if (type == tt.CaretEquals) {
      return '"^="';
    }
    if (type == tt.VerticalBar) {
      return '"|"';
    }
    if (type == tt.VerticalBarEquals) {
      return '"|="';
    }
    if (type == tt.VerticalBarVerticalBar) {
      return '"||"';
    }
    if (type == tt.VerticalBarVerticalBarEquals) {
      return '"||="';
    }
    if (type == tt.Tilde) {
      return '"~"';
    }

    if (type == tt.Eof) {
      return 'end of file';
    }

    assert(false, concat('don\'t know how to describe token with type ', type));
    return concat(type);
  }
}

TokenList tokenize(IntegerIterable source, String filename, Boolean internal) {
  Integer lineNo = 1;
  Integer colNo = 0;
  Integer startLineNo = -1;
  Integer startColNo = -1;

  Token createToken(tt typeArg, Anything dataArg) {
    return Token(
      typeArg,
      SourceLocation(startLineNo, startColNo, lineNo, colNo, filename),
      dataArg,
    );
  }

  Token createTokenWithBacktrack(tt typeArg, Anything dataArg) {
    return Token(
      typeArg, 
      SourceLocation(startLineNo, startColNo, lineNo, colNo - 1, filename),
      dataArg,
    );
  }

  ts state = ts.Top;
  Anything buffer;
  TokenList tokens = []:Token;
  for (ch in source) {
    colNo += 1;
    while (true) {
      if (state == ts.Top) {
        startLineNo = lineNo;
        startColNo = colNo;
        if (ch == 0x0a) { // LF
          lineNo += 1;
          colNo = 0;
          break;
        } else if (ch == 0x0d) {
          state = ts.CR;
          break;
        } else if (ch == 0x20) { // space
          break;
        } else if (ch == 0x21) { // !
          state = ts.Bang;
          break;
        } else if (ch == 0x22) { // "
          state = ts.DoubleQuoteString;
          buffer = []:String;
          break;
        } else if (ch == 0x25) { // %
          state = ts.Modulus;
          break;
        } else if (ch == 0x26) { // &
          state = ts.Ampersand;
          break;
        } else if (ch == 0x27) { // '
          state = ts.SingleQuoteString;
          buffer = []:String;
          break;
        } else if (ch == 0x28) { // (
          append(tokens, createToken(tt.OpenParen, null));
          break;
        } else if (ch == 0x29) { // )
          append(tokens, createToken(tt.CloseParen, null));
          break;
        } else if (ch == 0x2a) { // *
          state = ts.Asterisk;
          break;
        } else if (ch == 0x2b) { // +
          state = ts.Plus;
          break;
        } else if (ch == 0x2c) { // ,
          append(tokens, createToken(tt.Comma, null));
          break;
        } else if (ch == 0x2d) { // -
          state = ts.Minus;
          break;
        } else if (ch == 0x2e) { // .
          state = ts.Period;
          break;
        } else if (ch == 0x2f) { // /
          state = ts.Slash;
          break;
        } else if (ch == 0x30) { // 0
          state = ts.Zero;
          break;
        } else if (ch >= 0x31 && ch <= 0x39) { // 1-9
          state = ts.Integer;
          buffer = ch - 0x30;
          break;
        } else if (ch == 0x3a) { // :
          append(tokens, createToken(tt.Colon, null));
          break;
        } else if (ch == 0x3b) { // ;
          append(tokens, createToken(tt.Semicolon, null));
          break;
        } else if (ch == 0x3c) { // <
          state = ts.LessThan;
          break;
        } else if (ch == 0x3d) { // =
          state = ts.Equals;
          break;
        } else if (ch == 0x3e) { // >
          state = ts.GreaterThan;
          break;
        } else if ((ch >= 0x41 && ch <= 0x5a) || // A-Z
                   (ch >= 0x61 && ch <= 0x7a) || // a-z
                   (ch == 0x5f)) { // _
          state = ts.Identifier;
          buffer = [chr(ch)];
          break;
        } else if (ch == 0x5b) { // [
          append(tokens, createToken(tt.OpenBracket, null));
          break;
        } else if (ch == 0x5d) { // ]
          append(tokens, createToken(tt.CloseBracket, null));
          break;
        } else if (ch == 0x5e) { // ^
          state = ts.Caret;
          break;
        } else if (ch == 0x7b) { // {
          append(tokens, createToken(tt.OpenBrace, null));
          break;
        } else if (ch == 0x7c) { // |
          state = ts.VerticalBar;
          break;
        } else if (ch == 0x7d) { // }
          append(tokens, createToken(tt.CloseBrace, null));
          break;
        } else if (ch == 0x7e) { // ~
          append(tokens, createToken(tt.Tilde, null));
          break;
        } else {
          throw(concat('Unexpected character U+', padLeft(hex(ch), 4, "0"), ' at ', SourceLocation(startLineNo, startColNo, lineNo, colNo, filename), ': ', chr(ch)));
          break;
        }
      } else if (state == ts.Identifier) {
        if ((ch >= 0x30 && ch <= 0x39) || // 0-9
            (ch >= 0x41 && ch <= 0x5a) || // A-Z
            (ch >= 0x61 && ch <= 0x7a) || // a-z
            (ch == 0x5f) || // _
            (internal && ch == 0x23)) { /* # */
          append(buffer as StringList, chr(ch));
          break;
        } else {
          append(tokens, createTokenWithBacktrack(tt.Identifier, joinList(buffer as StringList)));
          state = ts.Top;
          // no break, reprocess in ts.Top
        }
      } else if (state == ts.SingleQuoteString) {
        if (ch == 0x27) { // '
          append(tokens, createToken(tt.String, joinList(buffer as StringList)));
          state = ts.Top; 
          break;
        } else if (ch == 0x5c) { // \
          state = ts.SingleQuoteStringEscape;
          break;
        } else {
          append(buffer as StringList, chr(ch));
          if (ch == 0x0a) { // LF
            lineNo += 1;
            colNo = 0;
          } else if (ch == 0x0d) { // CR
            state = ts.SingleQuoteStringCR;
          }
          break;
        }
      } else if (state == ts.SingleQuoteStringCR) {
        if (ch != 0x0a) {
          lineNo += 1;
          colNo = 0;
        }
        state = ts.SingleQuoteString;
        // no break, reprocess in ts.SingleQuoteString
      } else if (state == ts.SingleQuoteStringEscape) {
        if (ch == 0x6e) { // n
          append(buffer as StringList, '\n');
          state = ts.SingleQuoteString;
          break;
        } else if (ch == 0x72) { // r
          append(buffer as StringList, '\r');
          state = ts.SingleQuoteString;
          break;
        } else if (ch == 0x30) { // 0
          append(buffer as StringList, '\0');
          state = ts.SingleQuoteString;
          break;
        } else {
          append(buffer as StringList, chr(ch));
          state = ts.SingleQuoteString;
          if (ch == 0x0a) { // LF
            lineNo += 1;
            colNo = 0;
          } else if (ch == 0x0d) { // CR
            state = ts.SingleQuoteStringCR;
          }
          break;
        }
      } else if (state == ts.DoubleQuoteString) {
        if (ch == 0x22) { // "
          append(tokens, createToken(tt.String, joinList(buffer as StringList)));
          state = ts.Top;
          break;
        } else if (ch == 0x5c) { // \
          state = ts.DoubleQuoteStringEscape;
          break;
        } else {
          append(buffer as StringList, chr(ch));
          if (ch == 0x0a) { // LF
            lineNo += 1;
            colNo = 0;
          } else if (ch == 0x0d) { // CR
            state = ts.DoubleQuoteStringCR;
          }
          break;
        }
      } else if (state == ts.DoubleQuoteStringCR) {
        if (ch != 0x0a) {
          lineNo += 1;
          colNo = 0;
        }
        state = ts.DoubleQuoteString;
        // no break, reprocess in ts.DoubleQuoteString
      } else if (state == ts.DoubleQuoteStringEscape) {
        if (ch == 0x6e) { // n
          append(buffer as StringList, '\n');
          state = ts.DoubleQuoteString;
          break;
        } else if (ch == 0x72) { // r
          append(buffer as StringList, '\r');
          state = ts.DoubleQuoteString;
          break;
        } else if (ch == 0x30) { // 0
          append(buffer as StringList, '\0');
          state = ts.DoubleQuoteString;
          break;
        } else {
          append(buffer as StringList, chr(ch));
          state = ts.DoubleQuoteString;
          if (ch == 0x0a) { // LF
            lineNo += 1;
            colNo = 0;
          } else if (ch == 0x0d) { // CR
            state = ts.DoubleQuoteStringCR;
          }
          break;
        }
      } else if (state == ts.Zero) {
        if (ch >= 0x30 && ch <= 0x39) { // 0-9
          state = ts.Integer;
          buffer = 0;
          // no break, reprocess in ts.Integer
        } else if (ch == 0x58 || ch == 0x78) { // x, X
          state = ts.HexStart;
          break;
        } else {
          append(tokens, createTokenWithBacktrack(tt.Integer, 0));
          state = ts.Top;
          // no break, reprocess in ts.Top
        }
      } else if (state == ts.Integer) {
        if (ch >= 0x30 && ch <= 0x39) { // 0-9
          if ((buffer as Integer > 922337203685477580) || ((buffer as Integer == 922337203685477580) && (ch == 0x39))) {
            throw(concat('Invalid integer literal at ', SourceLocation(startLineNo, startColNo, lineNo, colNo, filename)));
          }
          buffer = buffer as Integer * 10 + (ch - 0x30);
          break;
        } else {
          append(tokens, createTokenWithBacktrack(tt.Integer, buffer));
          state = ts.Top;
          // no break, reprocess in ts.Top
        }
      } else if (state == ts.HexStart) {
        if ((ch >= 0x30 && ch <= 0x39) || // 0-9
            (ch >= 0x41 && ch <= 0x46) || // A-F
            (ch >= 0x61 && ch <= 0x66)) { // a-f
          state = ts.Hex;
          buffer = 0;
          // no break, reprocess in ts.Hex
        } else {
          throw(concat('Unexpected character ', padLeft(hex(ch), 4, "0"), ' in hex literal at ', SourceLocation(startLineNo, startColNo, lineNo, colNo, filename), ': ', chr(ch)));
        }
      } else if (state == ts.Hex) {
        if (ch >= 0x30 && ch <= 0x39) { // 0-9
          if ((buffer as Integer > 0x07ffffffffffffff) && ((buffer as Integer != 0x0800000000000000) || ch != 0x30)) {
            throw(concat('Invalid integer literal at ', SourceLocation(startLineNo, startColNo, lineNo, colNo, filename)));
          }
          buffer = buffer as Integer * 0x10 + (ch - 0x30);
          break;
        } else if (ch >= 0x41 && ch <= 0x46) { // A-F
          if (buffer as Integer > 0x07ffffffffffffff) {
            throw(concat('Invalid integer literal at ', SourceLocation(startLineNo, startColNo, lineNo, colNo, filename)));
          }
          buffer = buffer as Integer * 0x10 + (ch - 0x41) + 10;
          break;
        } else if (ch >= 0x61 && ch <= 0x66) { // a-f
          if (buffer as Integer > 0x07ffffffffffffff) {
            throw(concat('Invalid integer literal at ', SourceLocation(startLineNo, startColNo, lineNo, colNo, filename)));
          }
          buffer = buffer as Integer * 0x10 + (ch - 0x61) + 10;
          break;
        } else {
          append(tokens, createTokenWithBacktrack(tt.Integer, buffer));
          state = ts.Top;
          // no break, reprocess in ts.Top
        }

      } else if (state == ts.Period) {
        if (ch == 0x2e) { // .
          state = ts.PeriodPeriod;
          break;
        } else {
          append(tokens, createToken(tt.Period, null));
          state = ts.Top;
          // no break, reprocess in ts.Top
        }
      } else if (state == ts.PeriodPeriod) {
        if (ch == 0x2e) { // .
          append(tokens, createToken(tt.Ellipsis, null));
          state = ts.Top;
          break;
        } else {
          throw(concat('Unexpected character U+', padLeft(hex(ch), 4, "0"), ' at ', SourceLocation(startLineNo, startColNo, lineNo, colNo, filename), ': ', chr(ch)));
        }

      } else if (state == ts.Plus) {
        if (ch == 0x3d) { // =
          append(tokens, createToken(tt.PlusEquals, null));
          state = ts.Top;
          break;
        } else {
          append(tokens, createTokenWithBacktrack(tt.Plus, null));
          state = ts.Top;
          // no break, reprocess in ts.Top
        }
      } else if (state == ts.Minus) {
        if (ch == 0x3d) { // =
          append(tokens, createToken(tt.MinusEquals, null));
          state = ts.Top;
          break;
        } else {
          append(tokens, createTokenWithBacktrack(tt.Minus, null));
          state = ts.Top;
          // no break, reprocess in ts.Top
        }
      } else if (state == ts.Asterisk) {
        if (ch == 0x2a) { // *
          state = ts.AsteriskAsterisk;
          break;
        } else if (ch == 0x3d) { // =
          append(tokens, createToken(tt.AsteriskEquals, null));
          state = ts.Top;
          break;
        } else {
          append(tokens, createTokenWithBacktrack(tt.Asterisk, null));
          state = ts.Top;
          // no break, reprocess in ts.Top
        }
      } else if (state == ts.AsteriskAsterisk) {
        if (ch == 0x3d) { // =
          append(tokens, createToken(tt.AsteriskAsteriskEquals, null));
          state = ts.Top;
          break;
        } else {
          append(tokens, createTokenWithBacktrack(tt.AsteriskAsterisk, null));
          state = ts.Top;
          // no break, reprocess in ts.Top
        }
      } else if (state == ts.Slash) {
        if (ch == 0x2a) { // *
          state = ts.BlockComment;
          break;
        } else if (ch == 0x2f) { // /
          state = ts.LineComment;
          break;
        } else if (ch == 0x3d) { // =
          append(tokens, createToken(tt.SlashEquals, null));
          state = ts.Top;
          break;
        } else {
          append(tokens, createTokenWithBacktrack(tt.Slash, null));
          state = ts.Top;
          // no break, reprocess in ts.Top
        }
      } else if (state == ts.Modulus) {
        if (ch == 0x3d) { // =
          append(tokens, createToken(tt.ModulusEquals, null));
          state = ts.Top;
          break;
        } else {
          append(tokens, createTokenWithBacktrack(tt.Modulus, null));
          state = ts.Top;
          // no break, reprocess in ts.Top
        }
      } else if (state == ts.Equals) {
        if (ch == 0x3d) { // =
          append(tokens, createToken(tt.EqualsEquals, null));
          state = ts.Top;
          break;
        } else {
          append(tokens, createTokenWithBacktrack(tt.Equals, null));
          state = ts.Top;
          // no break, reprocess in ts.Top
        }
      } else if (state == ts.Bang) {
        if (ch == 0x3d) { // =
          append(tokens, createToken(tt.BangEquals, null));
          state = ts.Top;
          break;
        } else {
          append(tokens, createTokenWithBacktrack(tt.Bang, null));
          state = ts.Top;
          // no break, reprocess in ts.Top
        }
      } else if (state == ts.LessThan) {
        if (ch == 0x3c) { // <
          append(tokens, createToken(tt.LessThanLessThan, null));
          state = ts.Top;
          break;
        } else if (ch == 0x3d) { // =
          append(tokens, createToken(tt.LessThanEquals, null));
          state = ts.Top;
          break;
        } else {
          append(tokens, createTokenWithBacktrack(tt.LessThan, null));
          state = ts.Top;
          // no break, reprocess in ts.Top
        }
      } else if (state == ts.GreaterThan) {
        if (ch == 0x3d) { // =
          append(tokens, createToken(tt.GreaterThanEquals, null));
          state = ts.Top;
          break;
        } else if (ch == 0x3e) { // >
          append(tokens, createToken(tt.GreaterThanGreaterThan, null));
          state = ts.Top;
          break;
        } else {
          append(tokens, createTokenWithBacktrack(tt.GreaterThan, null));
          state = ts.Top;
          // no break, reprocess in ts.Top
        }
      } else if (state == ts.Ampersand) {
        if (ch == 0x26) { // &
          state = ts.AmpersandAmpersand;
          break;
        } else if (ch == 0x3d) { // =
          append(tokens, createToken(tt.AmpersandEquals, null));
          state = ts.Top;
          break;
        } else {
          append(tokens, createTokenWithBacktrack(tt.Ampersand, null));
          state = ts.Top;
          // no break, reprocess in ts.Top
        }
      } else if (state == ts.AmpersandAmpersand) {
        if (ch == 0x3d) { // =
          append(tokens, createToken(tt.AmpersandAmpersandEquals, null));
          state = ts.Top;
          break;
        } else {
          append(tokens, createTokenWithBacktrack(tt.AmpersandAmpersand, null));
          state = ts.Top;
          // no break, reprocess in ts.Top
        }
      } else if (state == ts.Caret) {
        if (ch == 0x3d) { // =
          append(tokens, createToken(tt.CaretEquals, null));
          state = ts.Top;
          break;
        } else {
          append(tokens, createTokenWithBacktrack(tt.Caret, null));
          state = ts.Top;
          // no break, reprocess in ts.Top
        }
      } else if (state == ts.VerticalBar) {
        if (ch == 0x7c) { // |
          state = ts.VerticalBarVerticalBar;
          break;
        } else if (ch == 0x3d) { // =
          append(tokens, createToken(tt.VerticalBarEquals, null));
          state = ts.Top;
          break;
        } else {
          append(tokens, createTokenWithBacktrack(tt.VerticalBar, null));
          state = ts.Top;
          // no break, reprocess in ts.Top
        }
      } else if (state == ts.VerticalBarVerticalBar) {
        if (ch == 0x3d) { // =
          append(tokens, createToken(tt.VerticalBarVerticalBarEquals, null));
          state = ts.Top;
          break;
        } else {
          append(tokens, createTokenWithBacktrack(tt.VerticalBarVerticalBar, null));
          state = ts.Top;
          // no break, reprocess in ts.Top
        }
      } else if (state == ts.LineComment) {
        if (ch == 0x0a) {
          state = ts.Top;
          // no break, reprocess in ts.Top
        } else if (ch == 0x0d) {
          state = ts.CR;
          break;
        } else {
          // Ignore until end of line.
          break;
        }
      } else if (state == ts.BlockComment) {
        if (ch == 0x0a) { // LF
          lineNo += 1;
          colNo = 0;
          break;
        } else if (ch == 0x0d) { // CR
          state = ts.BlockCommentCR;
          break;
        } else if (ch == 0x2a) { // *
          state = ts.BlockCommentAsterisk;
          break;
        } else {
          // Ignore until end of block.
          break;
        }
      } else if (state == ts.BlockCommentCR) {
        if (ch != 0x0a) {
          lineNo += 1;
          colNo = 0;
        }
        state = ts.BlockComment;
        // no break, reprocess in ts.BlockComment
      } else if (state == ts.BlockCommentAsterisk) {
        if (ch == 0x2f) { // /
          state = ts.Top;
          break;
        } else {
          state = ts.BlockComment;
          // no break, reprocess in ts.BlockComment
        }
      } else if (state == ts.CR) {
        if (ch != 0x0a) {
          lineNo += 1;
          colNo = 0;
        }
        state = ts.Top;
        // no break, reprocess in ts.Top
      } else {
        throw(concat('Unknown state ', state));
      }
    }
  }
  // Handle EOF
  if (state == ts.Top) {
    // no effect
  } else if (state == ts.Identifier) {
    append(tokens, createToken(tt.Identifier, joinList(buffer as WhateverList)));
  } else if (state == ts.SingleQuoteString) {
    throw(concat('Unterminated string at end of file started on ', SourceLocation(startLineNo, startColNo, startLineNo, startColNo, filename)));
  } else if (state == ts.SingleQuoteStringCR) {
    throw(concat('Unterminated string at end of file started on ', SourceLocation(startLineNo, startColNo, startLineNo, startColNo, filename)));
  } else if (state == ts.SingleQuoteStringEscape) {
    throw(concat('Unterminated string at end of file started on ', SourceLocation(startLineNo, startColNo, startLineNo, startColNo, filename)));
  } else if (state == ts.DoubleQuoteString) {
    throw(concat('Unterminated string at end of file started on ', SourceLocation(startLineNo, startColNo, startLineNo, startColNo, filename)));
  } else if (state == ts.DoubleQuoteStringCR) {
    throw(concat('Unterminated string at end of file started on ', SourceLocation(startLineNo, startColNo, startLineNo, startColNo, filename)));
  } else if (state == ts.DoubleQuoteStringEscape) {
    throw(concat('Unterminated string at end of file started on ', SourceLocation(startLineNo, startColNo, startLineNo, startColNo, filename)));
  } else if (state == ts.Zero) {
    append(tokens, createToken(tt.Integer, 0));
  } else if (state == ts.Integer) {
    append(tokens, createToken(tt.Integer, buffer));
  } else if (state == ts.HexStart) {
    throw(concat('Incomplete hex literal at end of ', filename));
  } else if (state == ts.Hex) {
    append(tokens, createToken(tt.Integer, buffer));

  } else if (state == ts.Period) {
    append(tokens, createToken(tt.Period, null));
  } else if (state == ts.PeriodPeriod) {
    throw(concat('Incomplete ellipsis at end of ', filename));

  } else if (state == ts.Plus) {
    append(tokens, createToken(tt.Plus, null));
  } else if (state == ts.Minus) {
    append(tokens, createToken(tt.Minus, null));
  } else if (state == ts.Asterisk) {
    append(tokens, createToken(tt.Asterisk, null));
  } else if (state == ts.AsteriskAsterisk) {
    append(tokens, createToken(tt.AsteriskAsterisk, null));
  } else if (state == ts.Slash) {
    append(tokens, createToken(tt.Slash, null));
  } else if (state == ts.Modulus) {
    append(tokens, createToken(tt.Modulus, null));
  } else if (state == ts.Equals) {
    append(tokens, createToken(tt.Equals, null));
  } else if (state == ts.Bang) {
    append(tokens, createToken(tt.Bang, null));
  } else if (state == ts.LessThan) {
    append(tokens, createToken(tt.LessThan, null));
  } else if (state == ts.GreaterThan) {
    append(tokens, createToken(tt.GreaterThan, null));
  } else if (state == ts.Ampersand) {
    append(tokens, createToken(tt.Ampersand, null));
  } else if (state == ts.AmpersandAmpersand) {
    append(tokens, createToken(tt.AmpersandAmpersand, null));
  } else if (state == ts.Caret) {
    append(tokens, createToken(tt.Caret, null));
  } else if (state == ts.VerticalBar) {
    append(tokens, createToken(tt.VerticalBar, null));
  } else if (state == ts.VerticalBarVerticalBar) {
    append(tokens, createToken(tt.VerticalBarVerticalBar, null));

  } else if (state == ts.LineComment) {
    // file terminated with line comment
  } else if (state == ts.BlockComment) {
    throw(concat('Unterminated block comment at end of file started on ', SourceLocation(startLineNo, startColNo, startLineNo, startColNo, filename)));
  } else if (state == ts.BlockCommentCR) {
    throw(concat('Unterminated block comment at end of file started on ', SourceLocation(startLineNo, startColNo, startLineNo, startColNo, filename)));
  } else if (state == ts.BlockCommentAsterisk) {
    throw(concat('Unterminated block comment at end of file started on ', SourceLocation(startLineNo, startColNo, startLineNo, startColNo, filename)));
  } else if (state == ts.CR) {
    // file terminated with newline
  } else {
    throw(concat('Unknown state ', state));
  }
  append(tokens, createToken(tt.Eof, null));
  return tokens;
}

class TokenConsumer extends Object {
  TokenList tokens;
  Integer index;

  SourceLocation lastLocation() {
    return tokens[index - 1].sourceLocation;
  }

  Boolean isNext(tt tokenType) {
    return tokens[index].type == tokenType;
  }
  
  Boolean isNext2(tt tokenType1, tt tokenType2) {
    return index + 1 < len(tokens)
        && tokens[index    ].type == tokenType1
        && tokens[index + 1].type == tokenType2;
  }
  
  Boolean isNextIdentifier(String identifier) {
    Token token = tokens[index];
    return token.type == tt.Identifier && token.data == identifier;
  }
  
  Boolean isNextKeyword(String keyword) {
    Token token = tokens[index];
    return token.type == tt.Keyword && token.data == keyword;
  }
  
  String butFound(String what, String description) {
    return concat('Expected ', what, ' but found ', description, ' at ', tokens[index].sourceLocation);
  }
  
  String expected(String what) {
    return butFound(what, concat(tokens[index]));
  }

  String readIdentifier() {
    if (!isNext(tt.Identifier)) {
      throw(concat(expected('identifier')));
    }
    String result = tokens[index].data as String;
    index = index + 1;
    return result;
  }
  
  String readString() {
    if (!isNext(tt.String)) {
      throw(concat(expected('string literal')));
    }
    String result = tokens[index].data as String;
    index = index + 1;
    return result;
  }
  
  Integer readInteger() {
    if (!isNext(tt.Integer)) {
      throw(concat(expected('integer literal')));
    }
    Integer result = tokens[index].data as Integer;
    index = index + 1;
    return result;
  }
  
  Null expectIdentifier(String identifier) {
    if (!isNext(tt.Identifier)) {
      throw(concat(expected(concat('identifier "', identifier, '"'))));
    }
    if (!isNextIdentifier(identifier)) {
      throw(concat(expected(concat('identifier "', identifier, '"'))));
    }
    index = index + 1;
  }
  
  Null expectKeyword(String keyword) {
    if (!isNext(tt.Keyword)) {
      throw(concat(expected(concat('keyword "', keyword, '"'))));
    }
    if (!isNextKeyword(keyword)) {
      throw(concat(expected(concat('keyword "', keyword, '"'))));
    }
    index = index + 1;
  }
  
  Null expectPunctuation(tt punctuation) {
    if (!isNext(punctuation)) {
      throw(concat(expected(concat(Token(punctuation, SourceLocation(-1, -1, -1, -1, ''), null)))));
    }
    index = index + 1;
  }
  
  Null constructor(TokenList tokensArg) {
    tokens = tokensArg;
    index = 0;
  }
}